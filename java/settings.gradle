// **********************************************************************
//
// Copyright (c) 2003-2016 ZeroC, Inc. All rights reserved.
//
// **********************************************************************

// Main source
include ':freeze'
project(':freeze').projectDir = new File('src/Freeze')

// Tests
include ':test'

// Map of dir->subdir for each of the demos.
def demos = [
        'Freeze' : ['bench', 'casino', 'library', 'phonebook', 'transform'],
        'Manual' : ['evictorFilesystem', 'mapFilesystem'],
]

demos.each {
        def dir = it.key
        def subdirs = it.value

        subdirs.each {
                // abc_def -> AbcDef
                def name = it.split('_').collect { it.capitalize() }.join()

                // A project in directory xyz is demoXyzAbcDef
                def projectName = ":demo${dir.capitalize()}${name}"
                include projectName

                // The directory is <dir>/<name>.
                project("${projectName}").projectDir = new File("demo/${dir}/${it}")
        }
}
